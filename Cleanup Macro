Option Explicit
Option Base 1

Sub MacmillanManuscriptCleanup()

'-----make sure Style template is attached
Dim keyStyle As Word.Style
Dim styleCheck As Boolean
On Error Resume Next
Set keyStyle = ActiveDocument.Styles("boldface characters (bf)")                '''Style from template to check against
styleCheck = keyStyle Is Nothing
If styleCheck Then
MsgBox "Requred style template 'macmillan.dotx' is not attached", , "Error"
Exit Sub
End If

'Clear Find & Replace Objects
Call ClearFindReplace

'-----------Remove White Space------------
Call RmWhiteSpace
'Clear Find & Replace Objects
Call ClearFindReplace

'-----------Replace Local Styles-----------
Call ReplaceLocalStyles
'Clear Find & Replace Objects
Call ClearFindReplace

'-----------Remove other Local Styles-----------
Call RmBadStyles
'Clear Find & Replace Objects
Call ClearFindReplace

End Sub

Sub RmWhiteSpace()

Dim activeRng As Range
Set activeRng = ActiveDocument.Range

Dim wsFindArray(7) As String              'number of items in array should be declared here
Dim wsReplaceArray(7) As String       'and here
Dim i As Long

wsFindArray(1) = "^t{1,}"             'tabs
wsFindArray(2) = "^l{1,}"               'manual line breaks
wsFindArray(3) = "^m{1,}"              'manual page breaks
wsFindArray(4) = " {2,}"               '2 or more spaces
wsFindArray(5) = "^13 "               'paragraph, space
wsFindArray(6) = " ^13"               'space, paragraph
wsFindArray(7) = "^13{2,}"          '2 or more paragraphs

wsReplaceArray(1) = ""
wsReplaceArray(2) = "^p"
wsReplaceArray(3) = "^p"
wsReplaceArray(4) = " "
wsReplaceArray(5) = "^p"
wsReplaceArray(6) = "^p"
wsReplaceArray(7) = "^p"

Application.ScreenUpdating = False

For i = 1 To UBound(wsFindArray())

With activeRng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = wsFindArray(i)
  .Replacement.Text = wsReplaceArray(i)
  .Forward = True
  .Wrap = wdFindContinue
  .Format = False
  .MatchCase = False
  .MatchWholeWord = False
  .MatchWildcards = True
  .MatchSoundsLike = False
  .MatchAllWordForms = False
  .Execute Replace:=wdReplaceAll
End With

Next

Application.ScreenUpdating = True
Application.ScreenRefresh

End Sub

Sub ReplaceLocalStyles()

Dim activeRng As Range
Set activeRng = ActiveDocument.Range

Dim styleFindArray(4) As Boolean              ' number of items in array should be declared here
Dim styleReplaceArray(4) As String         'and here
Dim j As Long

styleFindArray(1) = False        'Bold
styleFindArray(2) = False        'Italic
styleFindArray(3) = False        'Underline
styleFindArray(4) = False        'Smallcaps

styleReplaceArray(1) = ActiveDocument.Styles("boldface characters (bf)")
styleReplaceArray(2) = ActiveDocument.Styles("italic characters (ital)")
styleReplaceArray(3) = ActiveDocument.Styles("underscore characters (us)")
styleReplaceArray(4) = ActiveDocument.Styles("SMALL CAPS CHARACTERS (SC)")

Application.ScreenUpdating = False

For j = 1 To UBound(styleFindArray())

styleFindArray(j) = True

With activeRng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = ""
  .Forward = True
  .Wrap = wdFindContinue
  .Format = True
  .Font.Bold = styleFindArray(1)
  .Font.Italic = styleFindArray(2)
  .Font.Underline = styleFindArray(3)
  .Font.SmallCaps = styleFindArray(4)
  .Replacement.Style = styleReplaceArray(j)
  .Replacement.Font.Bold = False
  .Replacement.Font.Italic = False
  .Replacement.Font.Underline = False
  .Replacement.Font.SmallCaps = False
  .MatchCase = False
  .MatchWholeWord = False
  .MatchWildcards = False
  .MatchSoundsLike = False
  .MatchAllWordForms = False
  .Execute Replace:=wdReplaceAll
End With

styleFindArray(j) = False

Next

Application.ScreenUpdating = True
Application.ScreenRefresh

End Sub

Sub RmBadStyles()

Dim activeRng As Range
Set activeRng = ActiveDocument.Range

Dim badStyleArray(5) As Boolean              ' number of items in array should be declared here
Dim k As Long
                                                                            'notes = font colr doesn;t work, all caps doesn't matter
badStyleArray(1) = False        'Highlights
badStyleArray(2) = False        'Doublestrikethrough
badStyleArray(3) = False        'Strikethrough - double needed?
badStyleArray(4) = False        'Superscript
badStyleArray(5) = False        'Subscript

Application.ScreenUpdating = False

For k = 1 To UBound(badStyleArray())

badStyleArray(k) = True

With activeRng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = ""
  .Forward = True
  .Wrap = wdFindContinue
  .Format = True
  .Highlight = badStyleArray(1)
  .Replacement.Highlight = False
  .Font.DoubleStrikeThrough = badStyleArray(2)
  .Replacement.Font.DoubleStrikeThrough = False
  .Font.StrikeThrough = badStyleArray(3)
  .Replacement.Font.StrikeThrough = False
  .Font.Superscript = badStyleArray(4)
  .Replacement.Font.Superscript = False
  .Font.Subscript = badStyleArray(5)
  .Replacement.Font.Subscript = False
  .MatchCase = False
  .MatchWholeWord = False
  .MatchWildcards = False
  .MatchSoundsLike = False
  .MatchAllWordForms = False
  .Execute Replace:=wdReplaceAll
End With

badStyleArray(k) = False

Next

Application.ScreenUpdating = True
Application.ScreenRefresh

End Sub

Sub ClearFindReplace()

Dim clearRng As Range
Set clearRng = ActiveDocument.Words.First

With clearRng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = ""
  .Replacement.Text = ""
  .Forward = True
  .Wrap = wdFindStop
  .Format = False
  .MatchCase = False
  .MatchWholeWord = False
  .MatchWildcards = False
  .MatchSoundsLike = False
  .MatchAllWordForms = False
  .Execute
End With

End Sub